#include <std/assert>

public static final fun void_function() -> void:
    println("This is a void function")
    return  // null
end

public static final fun int_function() -> int:
    println("This function returns an integer")
    return 42
end

public static final fun string_function() -> str:
    println("This function returns a string")
    return ("Hello, World!")
end

public static final fun bool_function() -> bool:
    println("This function returns a boolean")
    return true
end

public static final fun list_function() -> list[int]:
    println("This function returns a list of integers")
    return [1, 2, 3, 4, 5]
end

fun test() end  // This is the minimal function.

fun parameter_function(a: int, b: str) -> void:
    println("This function takes parameters: a =", a, ", b =", b)
end

fun default_parameter_function(a: int = 10, b: str = "default") -> void:
    println("This function has default parameters: a =", a.to_string(), ", b =", b)
end

fun combined_parameter_function(a: int, b: str = "default") -> str:
    println("This function combines parameters and returns a string")
    return ("Combined: " + a.to_string() + ", " + b.to_string())
end

public static final fun recursive_function(n: int) -> int:
    if (n <= 1):
        return n
    else:
        return n + recursive_function(n - 1)
    end
end

public static final fun higher_order_function(func: function[int] -> int, value: int) -> int:
    println(func, value)
    println("This function takes another function as a parameter")
    return func(value)
end

private static mutable fun lower_order_function(value: int) -> int:
    println("This is a lower-order function")
    return value * 2
end

public static final fun mutable_arguments_function(mutable a: int, b: str) -> int:
    println("This function takes mutable arguments")
    println("a =", a, ", b =", b)
    a += 5
    println("After modification, a =", a)
    return a
end

fun variadic_function(*args: list[int]) -> int:
    println("This function takes a variable number of arguments")
    return sum(args) as int
end

fun unpacking_variadics(*args: list[any]) -> void:
    print(~args)
end

fun early_return_function(a: int) -> int:
    if (a < 0):
        return -1
    end
    return a * 2
    throw "This line is never reached"
end

fun tco_recursive_function(depth: int) -> void:
    if depth >= 1500:
        println(f'Reached {depth} without stack overflow!')
        return
    end
    tco_recursive_function(depth + 1)
end

// === Assertions ===

assert_eq!(void_function(), null)
assert_eq!(int_function(), 42)
assert_eq!(string_function(), "Hello, World!")
assert_eq!(bool_function(), true)
assert_eq!(list_function(), ([1, 2, 3, 4, 5]))
assert_eq!(parameter_function(5, "test"), null)
assert_eq!(default_parameter_function(), null)
assert_eq!(combined_parameter_function(15), "Combined: 15, default")
assert_eq!(combined_parameter_function(25, "override"), "Combined: 25, override")
assert_eq!(recursive_function(5), 15)
assert_eq!(recursive_function(0), 0)
assert_eq!(recursive_function(1), 1)
assert_eq!(higher_order_function(lower_order_function, 10), 20)
assert_eq!(mutable_arguments_function(10, "test"), 15)
assert_eq!(variadic_function(1, 2, 3, 4, 5), 15)
assert_eq!(unpacking_variadics("Hello", "World!"), null)
assert_eq!(early_return_function(10), 20)
assert_eq!(early_return_function(-5), -1)
assert_eq!(tco_recursive_function(0), null)