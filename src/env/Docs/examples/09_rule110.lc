#config debug = false

// Rule 110 Cellular Automaton in Lucia
// made by Markofwitch 2025, the developer of Lucia
// code inspired by https://github.com/tsoding/b/blob/main/examples/rule110.b

n: int = 100
base: list = array(n, 0)
base[n - 2] = 1

fun display(n: int):
    for (i in [0..n - 1]):
        if (base[i]):
            print("#", end="")
        else:
            print(".", end="")
        end
    end
    print()
end

fun next(n: int, mutable base: list) -> list:
    state: int = (base[0] bor (base[1] << 1))
    for (i in [2..n - 1]):
        state = (state << 1)
        state = (state bor base[i])
        state = (state band 7)
        base[i - 1] = ((110 >> state) band 1)
    end
    return (base)
end

for (i in [0..n]):
    base = next(n, base)
    display(n)
end
