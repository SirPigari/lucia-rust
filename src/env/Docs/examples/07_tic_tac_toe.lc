#config debug = false

// simple tic-tac-toe in Lucia
// made by Markofwitch 2025, the developer of Lucia

// btw the whole reason to make lucia was for simple stuff like this
// its not supposed to be fast or anything just easy to prototype simple stuff
// (or as a calculator)

fun print_board(board: list) -> void:
    print("╔═══════════╗")

    for (i in [0..2]):
        print("║ ", end="")

        for (j in [0..2]):
            idx: int = i * 3 + j
            cell: str = board[idx]

            if (cell == " "):
                styledprint(str(idx + 1), fg_color="#434343", end="")
            else:
                styledprint(get_player_string(cell), end="")
            end

            if (j < 2):
                print(" | ", end="")
            end
        end

        print(" ║")

        if (i < 2):
            print("║---+---+---║")
        end
    end

    print("╚═══════════╝")
end

fun check_win(board: list, player: str) -> bool:
    wins: list = [
        [0, 1, 2], [3, 4, 5], [6, 7, 8],  // rows
        [0, 3, 6], [1, 4, 7], [2, 5, 8],  // cols
        [0, 4, 8], [2, 4, 6]              // diagonals
    ]

    for (line in wins):
        if ((board[line[0]] == player) && (board[line[1]] == player) && (board[line[2]] == player)):
            return true
        end
    end
    return false
end

fun is_draw(board: list) -> bool:
    for (cell in board):
        if (cell == " "):
            return false
        end
    end
    return true
end

fun get_player_string(player: str) -> str:
    if (player == "X"):
        return styledstr("X", fg_color="#FF0000")
    else:
        return styledstr("O", fg_color="#0000FF")
    end
end

fun tic_tac_toe() -> void:
    board: list = array(9, " ")
    currentPlayer: str = "X"

    while (true):
        print_board(board)

        moveStr: str = input(f"Player {currentPlayer}, enter your move (1-9): ")

        if (len(moveStr) != 1):
            print("Invalid input, please enter a number from 1 to 9.")
            continue
        end

        moveCharCode: int = ord(moveStr[0])

        if (!((moveCharCode >= ord("1")) && (moveCharCode <= ord("9")))):
            print("Invalid input, please enter a number from 1 to 9.")
            continue
        end

        move: int = int(moveStr) - 1

        if (board[move] != " "):
            print("That spot is already taken, try again.")
            continue
        end

        board[move] = currentPlayer

        if (check_win(board, currentPlayer)):
            print_board(board)
            print("Player " + get_player_string(currentPlayer) + " wins!")
            break
        end

        if (is_draw(board)):
            print_board(board)
            print("It's a draw!")
            break
        end

        if (currentPlayer == "X"):
            currentPlayer = "O"
        else:
            currentPlayer = "X"
        end
    end
end

tic_tac_toe()
