import (*) from os.terminal
import (sleep) from time
import (exit) from os

// Conway's game of life in Lucia
// made by Markofwitch 2025, the developer of Lucia
// code inspired by https://gitlab.com/tsoding/porth/-/blob/master/examples/gol.porth
// Game originally created by John Conway in 1970

#config debug = false  // to hide debug info in terminal

sleep(500)

typedef Grid = list[list[bool; 20]; 20]

final width: int = 20
final height: int = 20

mutable grid: Grid = array(height, array(width, false))
mutable iteration: int = 0
mutable x: int = 0
mutable y: int = 0

fun main():
    enable_raw_mode()
    flush_input_buffer()
    mutable running: bool = true
    while (running):
        hide_cursor()
        draw(grid, x, y, f"Conway's Game of Life - Use Arrow Keys to Move, Space to Toggle Cell, Enter to Run, Esc to Exit.")
        flush()

        key := read_key(timeout=150)
        match key:
            KeyCode.Esc -> running = false,
            KeyCode.Up -> if y > 0: y -= 1 end,
            KeyCode.Down -> if y < height - 1: y += 1 end,
            KeyCode.Left -> if x > 0: x -= 1 end,
            KeyCode.Right -> if x < width - 1: x += 1 end,
            KeyCode.Space -> grid[y][x] = !grid[y][x],
            KeyCode.Enter -> run_simulation(),
            KeyCode.None -> null,
            _ -> null,
        end
    end
    disable_raw_mode()
    show_cursor()
    exit(0)
end

fun draw(grid: Grid, cx: int, cy: int, t: str) -> void:
    clear_screen()
    move_cursor(0, 0)
    println(t)
    for y in [0..height - 1]:
        for x in [0..width - 1]:
            c := if grid[y][x] then ' # ' else ' . '
            if x == cx && y == cy:
                styledprint(c, reverse=true, end="")
            else:
                print(c)
            end
        end
        println("\n")
    end
end

fun run_simulation():
    mutable last_iter: Grid = grid
    while true:
        mutable next: Grid = array(height, array(width, false))

        for y in [0..height - 1]:
            for x in [0..width - 1]:
                n: int = 0

                for dy in [-1, 0, 1]:
                    for dx in [-1, 0, 1]:
                        if dx == 0 && dy == 0:
                            continue
                        end
                        ny: int = y + dy
                        nx: int = x + dx

                        if nx >= 0 && nx < width && ny >= 0 && ny < height && grid[ny][nx]:
                            n += 1
                        end
                    end
                end

                next[y][x] = match (grid[y][x], n):
                    (true, 2) | (_, 3) -> true,
                    _ -> false,
                end
            end
        end
        grid = next
        iteration++
        draw(grid, 999, 999, f"Conway's Game of Life - Running Simulation ({iteration}) (Press Esc to Stop)")
        flush()
        if (read_key() == KeyCode.Esc) || grid.all((row) => row.all((cell) => !cell)) || (grid == last_iter):
            exit(0)
        end
        flush_input_buffer()
        last_iter = grid
    end
end

main()
